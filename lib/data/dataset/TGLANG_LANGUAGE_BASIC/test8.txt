100 CALL CLEAR
110 PRINT "YOU ARE ABOUT TO TEST YOUR  POWER OF ...": : : : : : : : :
120 FOR I=1 TO 50
130 PRINT TAB(I-INT(I/24)*24+3);"MINDSTORMING";
140 NEXT I
150 PRINT : : : : : : : : :
160 DIM A(50),COPY(50),B(50)
170 INPUT "#  OF POSITIONS (3-10): ":N
180 IF N<>INT(N)THEN 170
190 IF N>2 THEN 220
200 PRINT :"THAT'S NO CHALLENGE! PICK A LARGER NUMBER.": :
210 GOTO 170
220 IF N<11 THEN 260
230 PRINT :"THAT WILL BE RATHER         DIFFICULT. TRY AGAIN.": :
240 GOTO 170
250 PRINT : :
260 INPUT "HOW MANY DIGITS (3-10): ":C
270 IF C<>INT(C)THEN 260
280 IF C>2 THEN 310
290 PRINT :"YOU NEED MORE DIGITS THAN";C:"DON'T YOU WANT A FUN GAME?": :
300 GOTO 260
310 IF C<11 THEN 340
320 PRINT :"YOU CAN KEEP TRACK OF THAT  MANY DIGITS? WHAT A MIND!   TRY AGAIN -- I CAN'T HANDLE THAT MANY": :
330 GOTO 260
340 PRINT : :"SHALL I ALLOW REPEATING OF"
350 INPUT "DIGITS? (Y/N): ":REP$
360 IF SEG$(REP$,1,1)="Y" THEN 390
370 IF SEG$(REP$,1,1)<>"N" THEN 340
380 R=-1
390 IF (C<N)*R THEN 170
400 FOR I=1 TO N
410 RANDOMIZE
420 A(I)=INT(RND*C)+1
430 FOR J=1 TO I-1
440 IF (A(J)=A(I))*R THEN 410
450 NEXT J
460 COPY(I)=A(I)
470 NEXT I
480 NC=1000
490 PRINT : :"TRY #  CODE      RD/RP RD/WP":"-----  ----      ----- -----"
500 FOR I=1 TO NC
510 PRINT STR$(I);TAB(3);"-->  ";
520 FOR L=1 TO N
530 A(L)=COPY(L)
540 NEXT L
550 FOR J=1 TO N
560 CALL KEY(3,KEY,STATUS)
570 IF STATUS<1 THEN 560
580 IF KEY<>48 THEN 600
590 KEY=58
600 IF (KEY<49+KEY>48+C) THEN 560
610 PRINT CHR$(KEY);
620 B(J)=KEY-48
630 NEXT J
640 REM NOW FIGURE OUT THE SUMMARY
650 WH=0
660 BL=0
670 FOR J=1 TO N
680 IF A(J)*(A(J)=B(J))THEN 830
690 NEXT J
700 FOR J=1 TO N
710 FOR K=1 TO N
720 IF A(J)*(A(J)=B(K)) THEN 880
730 NEXT K
740 NEXT J
750 PRINT TAB(20);STR$(BL);TAB(26);STR$(WH)
760 NEXT I
770 PRINT "GEE, YOU'RE BAD!"
780 PRINT "WHY DON'T YOU TRY SOME"
790 PRINT "OTHER GAME?"
800 END
810 PRINT "MORE DIGITS -- MORE FUN"
820 GOTO 260
830 BL=BL+1
840 IF BL=N THEN 920
850 A(J)=0
860 B(J)=0
870 GOTO 690
880 WH=WH+1
890 A(J)=0
900 B(K)=0
910 GOTO 730
920 PRINT : :"CONGRATULATIONS! YOU WIN!": :
930 PRINT "YOUR RATING IS:"
940 RATING=(N+C/2)/I*3
950 IF RATING<=1 THEN 1010
960 IF RATING>=3 THEN 990
970 PRINT "NOT BAD.": :"KEEP WORKING AT IT THOUGH."
980 END
990 PRINT "FANTASTIC!": :"YOU DESERVE A    NOBEL PRIZE"
1000 END
1010 PRINT "TERRIBLE": :
1020 GOTO 770
